FUNCTION SetUp()
	Cls()

	# Connect host keyboard to hardware.
	SysPut("B", 5, true)
	# Disable Grid layer
	SysPut("B", 20, false)
	# Disable Pixel layer
	SysPut("B", 30, false)
	# Disable Sprite layer
	SysPut("B", 40, false)

	# Set color theme
	SysPut("N", 20, 0)
	SysPut("N", 21, 1)
	SysPut("N", 22, 1)
RESULT nothing

FUNCTION GetKeyPress()
	SET string pressed = KeyDown()
	WHILE pressed NEQ "" REPEAT
		# Return pressed key when it is
		# released.
		IF KeyUp() EQ pressed THEN
			OUTPUT pressed
		OK
	DONE
	OUTPUT ""
RESULT string

FUNCTION WaitKey()
	WHILE true REPEAT
		SET string key = GetKeyPress()
		IF key NEQ "" THEN
			OUTPUT key
		OK
	DONE
RESULT string

FUNCTION Join(list chars)
	READY string str

	EACH c IN chars REPEAT
		str = str + c
	DONE

	OUTPUT str
RESULT string

FUNCTION Prompt(string label)
	READY list chars
	READY string char

	WHILE char NEQ "ENTER" REPEAT
		Cls()
		Write(label, ": ")
		Write(Join(chars))
		WriteLn("")

		char = WaitKey()
		IF char EQ "BACKSPACE" THEN
			Pop(chars)
			char = ""
		OK

		IF char EQ "SPACE" THEN
			char = " "
		OK

		IF char NEQ "ENTER" AND char NEQ "" THEN
			Append(chars, char)
		OK
	DONE

	OUTPUT Join(chars)
RESULT string

FUNCTION Run()
	SetUp()

	SET string fname = Prompt("First name")
	SET string lname = Prompt("Last name")
	SET string age = Prompt("Age")
	SET string height = Prompt("Height (inches)")
	SET string weight = Prompt("Weight (pounds)")

	Cls()
	WriteLn("User Info")
	WriteLn("---------")
	WriteLn(lname + ", " + fname)
	WriteLn(age +  " years")
	WriteLn(weight + " lbs.")
	WriteLn(height + " in.")

RESULT nothing


Run()